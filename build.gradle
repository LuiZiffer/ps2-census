plugins {
	id 'java-library'
	id 'eclipse'
	id 'com.bmuschko.nexus' version '2.3.1'
	id 'maven-publish'
	//id 'com.autonomousapps.dependency-analysis' version '0.52.0'
}

version = '0.1.6'
group = 'io.github.luiziffer'
description = 'An unofficial PS2 Census API Wrapper for Java'

sourceCompatibility = 1.8


repositories {
	mavenLocal()
    mavenCentral()
    jcenter()
}

test {
	useJUnitPlatform()
	
	testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}


modifyPom {
     project {
     	name 'PS2 Census Java'
     	url "https://github.com/LuiZiffer/ps2-census"
     	description project.description
     	inceptionYear '2020'
     	
     	scm {
			url 'https://github.com/LuiZiffer/ps2-census'
			connection 'scm:git:git://github.com/luiziffer/ps2-census.git'
			developerConnection 'scm:git:ssh://git@github.com:luiziffer/ps2-census.git'
		}
	
	    licenses {
			license {
				name 'The MIT License (MIT)'
				url 'http://opensource.org/licenses/MIT'
				distribution 'repo'
			}
		}
	
		
	    developers {
			developer {
				id = 'luiziffer'
				name = 'Lui Ziffer'
				email = 'luiziff3r@gmail.com'
			}
		}
    }
}

nexus {
    sign = true
    //repositoryUrl = 'http://localhost:8081/nexus/content/repositories/internal/'
    //snapshotRepositoryUrl = 'http://localhost:8081/nexus/content/repositories/internal-snapshots/'
}



publishing {

    repositories {
        maven {
            name = "ps2-census"
            url = uri("https://maven.pkg.github.com/luiziffer/ps2-census")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java
			
			groupId project.group
            artifactId project.name
			
			pom {
				name = project.name
				description = 'An unofficial PS2 Census API Wrapper for Java'
				url = 'https://maven.pkg.github.com/luiziffer/ps2-census'
				
			}
        }
    }
}


dependencies {
	api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.3'
	api group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.3'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.0'
	
	api group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.1.0'
	implementation group: 'com.squareup.okio', name: 'okio', version: '2.2.2'
	
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
	
}
